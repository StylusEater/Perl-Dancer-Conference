#!/usr/bin/env perl

use Dancer ':script';
use Dancer::Plugin::DBIC;
binmode STDOUT, ":utf8";

my $schema = schema;

my $conference =
  rset('Conference')->find( { name => setting('conference_name') } );
die "conference not found" unless $conference;

my $surveys = setting('surveys');

foreach my $survey (@$surveys) {

    if ( $survey->{title} eq 'Talk review' ) {

        my $talks = rset('Talk')->search(
            { -bool => 'scheduled', conferences_id => $conference->id, },
            {
                prefetch => 'author'
            }
        );

        while ( my $talk = $talks->next ) {
            import_survey( $survey, $talk );
        }
    }
    else {
        import_survey($survey);
    }
}

sub import_survey {
    my ( $survey, $talk ) = @_;

    my $title = $survey->{title};

    if ($talk) {
        $title .= ": " . $talk->title . " by " . $talk->author->name;
        if ( $talk->lightning ) {
            $title = "Lightning " . $title;
        }
    }

    print "Processing survey with title: $title\n";

    my $survey_result = rset('Survey')
      ->find( { conferences_id => $conference->id, title => $title } );

    if ($survey_result) {
        print "Skipping: survey already in db\n";
        return;
    }

    my $author = rset('User')->find( { username => $survey->{author} } );
    die "author not found" unless $author;

    my $priority = 0;
    $priority = 200 unless $talk;
    $priority = 100 if ( $talk && $talk->duration > 60 );
    $priority = -100 if ( $talk && $talk->lightning );

    $survey_result = rset('Survey')->create(
        {
            conferences_id => $conference->id,
            title          => $title,
            author_id      => $author->id,
            priority       => $priority,
        }
    );

    if ( $talk ) {
        $talk->update({ survey_id => $survey_result->id });
    }

    my $survey_id = $survey_result->id;

    my $section_prio = 1000;

    foreach my $section ( @{ $survey->{sections} } ) {
        next unless $section->{section};

        my $title = $section->{section};
        if ($talk) {
            $title .= ": " . $talk->title;
        }

        my $survey_section = rset('SurveySection')->create(
            {
                title       => $title,
                description => $section->{text},
                priority    => $section_prio,
                survey_id   => $survey_id,
            }
        );
        $section_prio -= 10;

        my $question_prio = 1000;

        foreach my $question ( @{ $section->{questions} } ) {
            my $type = $question->{type} || "radio";
            my $survey_question = rset('SurveyQuestion')->create(
                {
                    title             => $question->{title},
                    description       => $question->{text} || '',
                    type              => $type,
                    priority          => $question_prio,
                    survey_section_id => $survey_section->id,
                    other             => $question->{other} || '',
                }
            );
            $question_prio -= 10;

            my $option_prio = 1000;

            foreach my $option ( @{ $question->{options} } ) {

                rset('SurveyQuestionOption')->create(
                    {
                        title              => $option,
                        priority           => $option_prio,
                        survey_question_id => $survey_question->id,
                    }
                );

                $option_prio -= 10;
            }
        }
    }
}
